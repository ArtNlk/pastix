set(PASTIX_VERSION @PASTIX_VERSION@)

# relocatable package
@PACKAGE_INIT@

set_and_check(PASTIX_BIN_DIR "@PACKAGE_BIN_INSTALL_DIR@")
set_and_check(PASTIX_INC_DIR "@PACKAGE_INC_INSTALL_DIR@")
set_and_check(PASTIX_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")

check_required_components(PASTIX)

# dependencies of PASTIX
include(CMakeFindDependencyMacro)

# Threads
if( NOT @BUILD_SHARED_LIBS@ )
  find_dependency(Threads)
endif()

# Math libraries
include("${CMAKE_CURRENT_LIST_DIR}/mTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/cblasTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/lapackeTargets.cmake")

# HwLoc libraries
if (@HWLOC_FOUND@ AND NOT @BUILD_SHARED_LIBS@)
  include("${CMAKE_CURRENT_LIST_DIR}/hwlocTargets.cmake")
endif()

# MPI library
if (@PASTIX_WITH_MPI@)
  include("${CMAKE_CURRENT_LIST_DIR}/mpicTargets.cmake")
  if (@PASTIX_WITH_FORTRAN@)
    include("${CMAKE_CURRENT_LIST_DIR}/mpifTargets.cmake")
  endif()
endif()

# GPU libraries
if (@PASTIX_WITH_CUDA@)
  include("${CMAKE_CURRENT_LIST_DIR}/cudaTargets.cmake")
  include("${CMAKE_CURRENT_LIST_DIR}/cublasTargets.cmake")
  include("${CMAKE_CURRENT_LIST_DIR}/pastix_kernels_cudaTargets.cmake")
endif()

# Runtime libraries
if (@PASTIX_WITH_PARSEC@)
  include("${CMAKE_CURRENT_LIST_DIR}/parsecTargets.cmake")
endif()
if (@PASTIX_WITH_STARPU@)
  include("${CMAKE_CURRENT_LIST_DIR}/starpuTargets.cmake")
endif()

# Ordering libraries
if (@PASTIX_ORDERING_SCOTCH@ AND NOT @BUILD_SHARED_LIBS@)
  include("${CMAKE_CURRENT_LIST_DIR}/scotchTargets.cmake")
endif()
if (@PASTIX_ORDERING_PTSCOTCH@ AND NOT @BUILD_SHARED_LIBS@)
  include("${CMAKE_CURRENT_LIST_DIR}/ptscotchTargets.cmake")
endif()
if (@PASTIX_ORDERING_METIS@ AND NOT @BUILD_SHARED_LIBS@)
  include("${CMAKE_CURRENT_LIST_DIR}/metisTargets.cmake")
endif()

# add the targets file
if ( @SPM_FOUND@ )
  include("@SPM_DIR@/spmTargets.cmake")
  if (@PASTIX_WITH_FORTRAN@)
    include("@SPM_DIR@/spmfTargets.cmake")
  endif()
else()
  include("${CMAKE_CURRENT_LIST_DIR}/../spm/spmTargets.cmake")
  if (@PASTIX_WITH_FORTRAN@)
    include("${CMAKE_CURRENT_LIST_DIR}/../spm/spmfTargets.cmake")
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/pastix_kernelsTargets.cmake")
if (@PASTIX_WITH_PARSEC@)
  include("${CMAKE_CURRENT_LIST_DIR}/pastix_parsecTargets.cmake")
endif()
if (@PASTIX_WITH_STARPU@)
  include("${CMAKE_CURRENT_LIST_DIR}/pastix_starpuTargets.cmake")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/pastixTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/pastixfTargets.cmake")
