set(PASTIX_VERSION @PASTIX_VERSION@)

# relocatable package
@PACKAGE_INIT@

set_and_check(PASTIX_BIN_DIR "@PACKAGE_BIN_INSTALL_DIR@")
set_and_check(PASTIX_INC_DIR "@PACKAGE_INC_INSTALL_DIR@")
set_and_check(PASTIX_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")

check_required_components(PASTIX)

# need MORSE Find modules: necessary files must be distributed in the install path
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/find")

# dependencies of PASTIX
include(CMakeFindDependencyMacro)

# Math libraries
find_dependency(M)
find_dependency(CBLAS REQUIRED)
find_dependency(LAPACKE REQUIRED)

# HwLoc libraries
if (@HWLOC_FOUND@)
  find_dependency(HWLOC REQUIRED)
endif()

# MPI library
if (@PASTIX_WITH_MPI@)
  find_dependency(MPI REQUIRED)
endif()

# GPU libraries
if (@PASTIX_WITH_CUDA@)
  find_dependency(CUDA REQUIRED)
  include("${CMAKE_CURRENT_LIST_DIR}/pastix_kernels_cudaTargets.cmake")
endif()

# Runtime libraries
if (@PASTIX_WITH_PARSEC@)
  find_dependency(PARSEC REQUIRED)
endif()
if (@PASTIX_WITH_STARPU@)
  find_package(STARPU @PASTIX_STARPU_VERSION@ REQUIRED)
endif()

# Ordering libraries
if (@PASTIX_ORDERING_SCOTCH@)
  find_dependency(SCOTCH REQUIRED)
endif()
if (@PASTIX_ORDERING_PTSCOTCH@)
  find_dependency(PTSCOTCH REQUIRED)
endif()
if (@PASTIX_ORDERING_METIS@)
  find_dependency(METIS REQUIRED)
endif()

# add the targets file
if ( @SPM_FOUND@ )
  find_dependency( SPM REQUIRED )
else()
  include("${CMAKE_CURRENT_LIST_DIR}/../spm/spmTargets.cmake")
  if (@PASTIX_WITH_FORTRAN@)
    include("${CMAKE_CURRENT_LIST_DIR}/../spm/spmfTargets.cmake")
  endif()
endif()

include("${CMAKE_CURRENT_LIST_DIR}/pastix_kernelsTargets.cmake")
if (@PASTIX_WITH_PARSEC@)
  include("${CMAKE_CURRENT_LIST_DIR}/pastix_parsecTargets.cmake")
endif()
if (@PASTIX_WITH_STARPU@)
  include("${CMAKE_CURRENT_LIST_DIR}/pastix_starpuTargets.cmake")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/pastixTargets.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/pastixfTargets.cmake")
